<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_VCN_VAT590" Id="{4758ecab-cc0c-4c37-8981-9d3a1c0c082a}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function implements the Basic functions for the VAT590 needle valve*)
FUNCTION_BLOCK FB_VCN_VAT590 EXTENDS FB_Valve
VAR_INPUT
	bExtIlkOK : BOOL;        // External Interlock, SET to TRUE if not used
	IG : ST_VG;              // Input from a cold cathode gauge used in control loop
END_VAR
VAR_OUTPUT
	{attribute 'pytmc' := '
    pv:
	'}
	stVcnVat590	: ST_VCN_VAT590;       // Needle valve VAT590 structure
END_VAR
VAR CONSTANT
	cOpenPosition : REAL := 1E+5;      // Default position setpoint upper scale value
	cClosePosition : REAL := 0.0;      // Default position setpoint lower scale value
	cPresSPUpLim : REAL := 1E+6;       // Default pressure setpoint upper scale value
	cPresSPLowLim : REAL := 0;         // Default pressure setpoint lower scale value
	cPressCtrl : ST_CoEIndSub := (nIndex := 16#2199, nSubIndex := 0); // Selected pressure controller CoE register
	cSensorDelay_AD : ST_CoEIndSub := (nIndex := 16#219A, nSubIndex := 0); // Adaptive DS controller Sensor Delay CoE register
	cRampTime_AD : ST_CoEIndSub := (nIndex := 16#219B, nSubIndex := 0); // Adaptive DS controller Ramp Time CoE register
	cRampMode_AD : ST_CoEIndSub := (nIndex := 16#219C, nSubIndex := 0); // Adaptive DS controller Ramp Mode CoE register
	cGainFactor_AD : ST_CoEIndSub := (nIndex := 16#219E, nSubIndex := 0); // Adaptive DS controller Gain factor CoE register
	cRampTime_FIX1 : ST_CoEIndSub := (nIndex := 16#21A5, nSubIndex := 0); // Fixed 1 controller Ramp Time CoE register
	cRampMode_FIX1 : ST_CoEIndSub := (nIndex := 16#21A6, nSubIndex := 0); // Fixed 1 controller Ramp Mode CoE register
	cCtrlDir_FIX1 : ST_CoEIndSub := (nIndex := 16#21A7, nSubIndex := 0); // Fixed 1 controller Control Direction CoE register
	cPGain_FIX1 : ST_CoEIndSub := (nIndex := 16#21A8, nSubIndex := 0); // Fixed 1 controller P Gain CoE register
	cIGain_FIX1 : ST_CoEIndSub := (nIndex := 16#21A9, nSubIndex := 0); // Fixed 1 controller I Gain CoE register
	cRampTime_FIX2 : ST_CoEIndSub := (nIndex := 16#21AF, nSubIndex := 0); // Fixed 2 controller Ramp Time CoE register
	cRampMode_FIX2 : ST_CoEIndSub := (nIndex := 16#21B0, nSubIndex := 0); // Fixed 2 controller Ramp Mode CoE register
	cCtrlDir_FIX2 : ST_CoEIndSub := (nIndex := 16#21B1, nSubIndex := 0); // Fixed 2 controller Control Direction CoE register
	cPGain_FIX2 : ST_CoEIndSub := (nIndex := 16#21B2, nSubIndex := 0); // Fixed 2 controller P Gain CoE register
	cIGain_FIX2 : ST_CoEIndSub := (nIndex := 16#21B3, nSubIndex := 0); // Fixed 2 controller I Gain CoE register
	cRampTime_SP : ST_CoEIndSub := (nIndex := 16#21B9, nSubIndex := 0); // Soft Pump controller Ramp Time CoE register
	cRampMode_SP : ST_CoEIndSub := (nIndex := 16#21BA, nSubIndex := 0); // Soft Pump controller Ramp Time CoE register
	cPGain_SP : ST_CoEIndSub := (nIndex := 16#21BC, nSubIndex := 0); // Soft Pump controller Ramp Time CoE register
END_VAR
VAR
	fCalcPosSP : REAL := 0;
	fCalcPresSP : REAL := 0;
	eValveModeSP : E_ControlModeSP;
	fReqPos : REAL;                    // Requested position internal variable
	fReqPres : REAL;                   // Requested pressure internal variable
	fPresLowLim : REAL := 1E-10;       // Pressure sensor lower scale value
	fSlope : REAL := 1E+5;             // Slope (Units per decade) used for conversion from Torr units
	fVbase : REAL := 1E+5;             // Vbase (Units per decade) defined for how much the curve is shifted
	bRemAcsToggle : BOOL;              // Remote access control bit
	//timer for zero function 
	tZeroTogg : TON := (PT := T#1S);
	//timer for remote function
	tRemAcsTogg : TON := (PT := T#500MS);
	//used for logging
	ePreviousState : E_VCN_VAT590;
	
	(*CoE Interface*)
	(*Link this to your VAT590 reference variables under InfoData.*)
	stVat590PlcDriveRef AT %I*: ST_PlcDriveRef;
	stVat590DriveRef : ST_DriveRef := (
		sNetId := F_CreateAmsNetId(stVat590PlcDriveRef.aNetId),
		nSlaveAddr := stVat590PlcDriveRef.nSlaveAddr,
		nDriveNo := stVat590PlcDriveRef.nDriveNo,
		nDriveType := stVat590PlcDriveRef.nDriveType
	);

	fbCoeRead : FB_CoERead_ByDriveRef := (
		stDriveRef := stVat590DriveRef
	);
	fbCoeWrite : FB_CoEWrite_ByDriveRef := (
		stDriveRef := stVat590DriveRef
	);
	
	stCoeParameters : ST_VAT590_COE;   //Internal storage of CoE parameters
	nCaseReadCoe : UINT := 1;          //Iterator for going through all CoE parameters
	rtCoeReset : R_TRIG;               //Rising edge trigger for reset CoE Interface error
	ftCoeReadBusy : F_TRIG;            //Falling edge trigger for CoE read new data
	ftCoeWriteBusy : F_TRIG;           //Falling edge trigger for CoE write data
	bFirstRead : BOOL := TRUE;
	
	(*VAT590 Ethercat IO Interface*)
	//Ethercat Interface Inputs
	i_nRawPosition       AT %I* : DINT;               // Position readback from the valve
	i_nRawPressure       AT %I* : DINT;               // Pressure readback from the valve
	i_eControlMode       AT %I* : E_ControlMode;      // Valve control mode readback
	i_eFatalError        AT %I* : E_FatalErrors;      // Valve Fatal Errors readback
	i_stGenStatus        AT %I* : ST_GeneralStatus;   // Valve General Status readback
	i_stGenWarnings      AT %I* : ST_GeneralWarnings; // Valve General Warnings readback
	i_stExtnWarnings     AT %I* : ST_ExtendedWarnings;// Valve Extended Warnings readback
	//Ethercat Interface Outputs
	o_nRawPositionSP     AT %Q* : DINT;               // Position setpoint
	o_nRawPressureSP     AT %Q* : DINT;               // Pressure setpoint
	o_eCtrlModeSP        AT %Q* : E_ControlModeSP;    // Valve control mode setpoint
	o_stGenCtrlSP        AT %Q* : ST_GeneralControlSP;// General Control setpoint
END_VAR
VAR PERSISTENT
    fUpperLimit	: REAL;                // Max position in % that can be requested
	fPresMaxLim : REAL;                // Max pressure in Torr that can be requested
	fPresRbMaxLim : REAL;               // Max pressure in Torr read by chamber gauge, valve will close above the pressure.
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Needle valve control FB
J. Govednik 2021-11-22

This FB should be used as a low level control block.

It provides:
Valve position ceiling
Interlock
Scaling

It could be used for:
Valve position/flow linearization
*)

(*Read from EPICS and Valve Ethercat interface*)
ReadFromInput();

(*Checking which Control mode is selected*)
IF bExtIlkOK THEN
	CASE stVcnVat590.eValveControl OF
		E_VCN_VAT590.OpenValve:
			eValveModeSP := E_ControlModeSP.OpenValve;
		E_VCN_VAT590.CloseValve:
			eValveModeSP := E_ControlModeSP.CloseValve;
		E_VCN_VAT590.ManualControl:
		    IF IG.xPRESS_OK AND IG.rPRESS <= fPresRbMaxLim THEN
				eValveModeSP := E_ControlModeSP.ManualControl;
			ELSE
				eValveModeSP := E_ControlModeSP.CloseValve; // Close valve if something is wrong with the gauge
				stVcnVat590.eValveControl := E_VCN_VAT590.CloseValve;
			END_IF	
		E_VCN_VAT590.PressureControl:
			IF IG.xPRESS_OK AND IG.rPRESS <= fPresRbMaxLim THEN
				eValveModeSP := E_ControlModeSP.PressureControl;
			ELSE
				eValveModeSP := E_ControlModeSP.CloseValve; // Close valve if something is wrong with the gauge
				stVcnVat590.eValveControl := E_VCN_VAT590.CloseValve;
			END_IF
		E_VCN_VAT590.HoldValve:
			IF stVcnVat590.stValveStatus.eControlMode = E_ControlMode.ManualControl OR
				stVcnVat590.stValveStatus.eControlMode = E_ControlMode.PressureControl THEN
				eValveModeSP := E_ControlModeSP.HoldValve;
			END_IF
	END_CASE
ELSE
	fReqPos := 0;
	eValveModeSP := E_ControlModeSP.CloseValve;
	stVcnVat590.eValveControl := E_VCN_VAT590.CloseValve;
END_IF

(*Requested Position calculation*)
fReqPos := LIMIT(0, fReqPos, fUpperLimit);
fCalcPosSP := fReqPos * (cOpenPosition-cClosePosition)/100 + cClosePosition;
fCalcPosSP := LIMIT(cClosePosition, fCalcPosSP, cOpenPosition); //The requested position SP should remain within this range

(*Pressure calculation*)
//Limit the input from EPICS between upper and lower limit defined for seleceted pressure sensor
fReqPres := LIMIT(fPresLowLim, fReqPres, fPresMaxLim);

// Calculate pressure readback and pressure setpoint according to selected pressure gauge
CASE stVcnVat590.ePressureSensor OF
	E_VAT590_PressureSensor.MKS500:
		fPresLowLim := 1.0E-10;
		fSlope := 1E+5;
		fVbase := 1E+5;
		//Calcuate pressure setpoint
		fCalcPresSP := fSlope * LOG(fReqPres/fPresLowLim) + fVbase;
		fCalcPresSP := LIMIT(cPresSPLowLim, fCalcPresSP, cPresSPUpLim); //The requested pressure SP should remain within this range
		//Calculate pressure readback in Torr units
		stVcnVat590.fPressure := LREAL_TO_REAL(EXPT(10,((DINT_TO_REAL(i_nRawPressure)-fVbase)/fSlope + LOG(fPresLowLim))));
	ELSE
		fCalcPresSP := 0;
		stVcnVat590.fPressure := 0;
END_CASE

(*Calculated Pressure SP and Pressure readback is 0 if Pressure sensor is not working*) 
IF NOT (IG.xPRESS_OK AND IG.rPRESS < fPresRbMaxLim ) THEN
	fCalcPresSP := 0;
	stVcnVat590.fPressure := 0;
END_IF

(*Zero function*)
IF stVcnVat590.stValveStatus.bZeroDisabled OR tZeroTogg.Q THEN
	stVcnVat590.bZero := FALSE;
END_IF
tZeroTogg.IN := stVcnVat590.bZero;
tZeroTogg();

(*Set Remote Access*)
IF stVcnVat590.stValveStatus.eRemoteStatus <> stVcnVat590.eRemoteControl THEN
	tRemAcsTogg.IN := TRUE;
	IF tRemAcsTogg.Q THEN
		bRemAcsToggle := NOT bRemAcsToggle;
		tRemAcsTogg.IN := FALSE;
	END_IF
END_IF
tRemAcsTogg();

(*Reset Coe Interface error*)
rtCoeReset(CLK := stVcnVat590.bResetCoeError);
IF rtCoeReset.Q THEN
	stVcnVat590.bResetCoeError := FALSE;
END_IF

(*Read CoE Parameters*)
ReadCoeParameters();

(*Write CoE Parameters*)
WriteCoeParameters();

(*Write to EPICS and Valve Ethercat interface*)
WriteToOutput();

(*Load or save the persistent variables*)
LoadPersistentData();

(*Logging*)
Logger();]]></ST>
    </Implementation>
    <Action Name="LoadPersistentData" Id="{e5168615-6ef3-4946-b318-5dac9816d744}">
      <Implementation>
        <ST><![CDATA[(*On first PLC pass, load the persistent value into the structrue variable*)
IF (bRestorePersistentData) THEN
	bRestorePersistentData	:= FALSE;
	IF (fUpperLimit <> 0) THEN
		stVcnVat590.fPosSetLimit := fUpperLimit;
	END_IF;
	IF (fPresMaxLim <> 0) THEN
		stVcnVat590.fPresSetLimit := fPresMaxLim;
	END_IF;
	IF (fPresRbMaxLim <> 0) THEN
		stVcnVat590.fPresRdbkLimit := fPresRbMaxLim;
	END_IF;    
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="Logger" Id="{e05977be-b3df-4692-b1e1-f426b01665a8}">
      <Implementation>
        <ST><![CDATA[// ILK logger
IF NOT (stVcnVat590.bIlkOK ) AND NOT(ePreviousState = E_VCN_VAT590.CloseValve) THEN
 		fbLogger(sMsg:='Lost interlock ok bit while valve was open.', eSevr:=TcEventSeverity.Critical);  	
END_IF


//Control STATE Logger  
IF ePreviousState <> stVcnVat590.eValveControl THEN
	  CASE stVcnVat590.eValveControl OF
		E_VCN_VAT590.ManualControl:
			fbLogger(sMsg:='Valve in manual control mode.', eSevr:=TcEventSeverity.Warning);
		E_VCN_VAT590.HoldValve:
			fbLogger(sMsg:='Valve in hold control mode.', eSevr:=TcEventSeverity.Warning);
		E_VCN_VAT590.OpenValve:
			fbLogger(sMsg:='Valve Open.', eSevr:=TcEventSeverity.Info);
		E_VCN_VAT590.PressureControl:
			fbLogger(sMsg:='Valve in pressure control mode.', eSevr:=TcEventSeverity.Info);
		E_VCN_VAT590.CloseValve:
			fbLogger(sMsg:='Valve closed.', eSevr:=TcEventSeverity.Info);
	  END_CASE
	  ePreviousState := stVcnVat590.eValveControl;  
  END_IF
 

// Log valve open
tAction(CLK:= (stVcnVat590.eValveControl = E_VCN_VAT590.OpenValve) );
IF tAction.Q THEN fbLogger(sMsg:='Valve commanded to fully open', eSevr:=TcEventSeverity.Info); END_IF
]]></ST>
      </Implementation>
    </Action>
    <Action Name="ReadCoeParameters" Id="{dceab739-9779-4d3e-8c12-eb434a1eb90a}">
      <Implementation>
        <ST><![CDATA[CASE nCaseReadCoe OF
	0:
		//fbCoeRead.bExecute := FALSE;
		stVcnVat590.bCoeError := TRUE;
		IF rtCoeReset.Q THEN
			nCaseReadCoe := 1;
			stVcnVat590.bCoeError := FALSE;
			bFirstRead := TRUE; // Set bFirstRead after Coe interface reset
		END_IF
	1:
		fbCoeRead.nIndex := cPressCtrl.nIndex;
		fbCoeRead.nSubIndex := cPressCtrl.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nPressCtrl);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nPressCtrl);
		fbCoeRead.bExecute := TRUE;
	2:
		fbCoeRead.nIndex := cSensorDelay_AD.nIndex;
		fbCoeRead.nSubIndex := cSensorDelay_AD.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fSensorDelay_AD);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fSensorDelay_AD);
		fbCoeRead.bExecute := TRUE;
	3:
		fbCoeRead.nIndex := cRampTime_AD.nIndex;
		fbCoeRead.nSubIndex := cRampTime_AD.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_AD);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_AD);
		fbCoeRead.bExecute := TRUE;
	4:
		fbCoeRead.nIndex := cRampMode_AD.nIndex;
		fbCoeRead.nSubIndex := cRampMode_AD.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_AD);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_AD);
		fbCoeRead.bExecute := TRUE;
	5:
		fbCoeRead.nIndex := cGainFactor_AD.nIndex;
		fbCoeRead.nSubIndex := cGainFactor_AD.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fGainFactor_AD);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fGainFactor_AD);
		fbCoeRead.bExecute := TRUE;
	6:
		fbCoeRead.nIndex := cRampTime_FIX1.nIndex;
		fbCoeRead.nSubIndex := cRampTime_FIX1.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_FIX1);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_FIX1);
		fbCoeRead.bExecute := TRUE;
	7:
		fbCoeRead.nIndex := cRampMode_FIX1.nIndex;
		fbCoeRead.nSubIndex := cRampMode_FIX1.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_FIX1);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_FIX1);
		fbCoeRead.bExecute := TRUE;
	8:
		fbCoeRead.nIndex := cCtrlDir_FIX1.nIndex;
		fbCoeRead.nSubIndex := cCtrlDir_FIX1.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nCtrlDir_FIX1);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nCtrlDir_FIX1);
		fbCoeRead.bExecute := TRUE;
	9:
		fbCoeRead.nIndex := cPGain_FIX1.nIndex;
		fbCoeRead.nSubIndex := cPGain_FIX1.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fPGain_FIX1);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_FIX1);
		fbCoeRead.bExecute := TRUE;
	10:
		fbCoeRead.nIndex := cIGain_FIX1.nIndex;
		fbCoeRead.nSubIndex := cIGain_FIX1.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fIGain_FIX1);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fIGain_FIX1);
		fbCoeRead.bExecute := TRUE;
	11:
		fbCoeRead.nIndex := cRampTime_FIX2.nIndex;
		fbCoeRead.nSubIndex := cRampTime_FIX2.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_FIX2);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_FIX2);
		fbCoeRead.bExecute := TRUE;
	12:
		fbCoeRead.nIndex := cRampMode_FIX2.nIndex;
		fbCoeRead.nSubIndex := cRampMode_FIX2.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_FIX2);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_FIX2);
		fbCoeRead.bExecute := TRUE;
	13:
		fbCoeRead.nIndex := cCtrlDir_FIX2.nIndex;
		fbCoeRead.nSubIndex := cCtrlDir_FIX2.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nCtrlDir_FIX2);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nCtrlDir_FIX2);
		fbCoeRead.bExecute := TRUE;
	14:
		fbCoeRead.nIndex := cPGain_FIX2.nIndex;
		fbCoeRead.nSubIndex := cPGain_FIX2.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fPGain_FIX2);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_FIX2);
		fbCoeRead.bExecute := TRUE;
	15:
		fbCoeRead.nIndex := cIGain_FIX2.nIndex;
		fbCoeRead.nSubIndex := cIGain_FIX2.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fIGain_FIX2);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fIGain_FIX2);
		fbCoeRead.bExecute := TRUE;
	16:
		fbCoeRead.nIndex := cRampTime_SP.nIndex;
		fbCoeRead.nSubIndex := cRampTime_SP.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_SP);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_SP);
		fbCoeRead.bExecute := TRUE;
	17:
		fbCoeRead.nIndex := cRampMode_SP.nIndex;
		fbCoeRead.nSubIndex := cRampMode_SP.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_SP);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_SP);
		fbCoeRead.bExecute := TRUE;
	18:
		fbCoeRead.nIndex := cPGain_SP.nIndex;
		fbCoeRead.nSubIndex := cPGain_SP.nSubIndex;
		fbCoeRead.pDstBuf := ADR(stVcnVat590.stCoeInterface.fPGain_SP);
		fbCoeRead.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_SP);
		fbCoeRead.bExecute := TRUE;
END_CASE

ftCoeReadBusy(CLK:=fbCoeRead.bBusy);

IF ftCoeReadBusy.Q THEN
	fbCoeRead.bExecute := FALSE;
	IF NOT fbCoeRead.bError THEN
		nCaseReadCoe := nCaseReadCoe + 1;
		IF nCaseReadCoe > 18 THEN
			nCaseReadCoe := 1;
			bFirstRead := FALSE; // Reset bFirstRead after successfull readout of all parameters
		END_IF
	END_IF
END_IF

fbCoeRead();
stCoeParameters := stVcnVat590.stCoeInterface; //Save current values to internal structure]]></ST>
      </Implementation>
    </Action>
    <Action Name="ReadFromInput" Id="{2b46db7d-12fe-424a-afdf-e2d60ed01bdb}">
      <Implementation>
        <ST><![CDATA[(*Read from EPICS*)
fUpperLimit := LIMIT(0, stVcnVat590.fPosSetLimit, 100);
fPresMaxLim := LIMIT(1.0E-10, stVcnVat590.fPresSetLimit, 1.0E-2);
fPresRbMaxLim := LIMIT(1.0E-10, stVcnVat590.fPresRdbkLimit, 1.0E-2);
fReqPos := LIMIT(0, stVcnVat590.fReqPosition, 100);
fReqPres := LIMIT(1.0E-10, stVcnVat590.fReqPressure, 1.0E-2);

(*Read from VAT valve*)
stVcnVat590.stValveStatus.eControlMode := i_eControlMode;
stVcnVat590.stValveStatus.eFatalErrors := i_eFatalError;
stVcnVat590.nRawPosition := i_nRawPosition;
stVcnVat590.nRawPressure := i_nRawPressure;
//Mapping General statuses
stVcnVat590.stValveStatus.bFieldBusDatavalid := i_stGenStatus.Bit0FieldBusDatavalid;
stVcnVat590.stValveStatus.bZeroExecuted := i_stGenStatus.Bit1ZeroExecuted;
stVcnVat590.stValveStatus.bPingPongRxBit := i_stGenStatus.Bit2PingPongRxBit;
stVcnVat590.stValveStatus.bPressureSimulation := i_stGenStatus.Bit3PressureSimulation;
stVcnVat590.stValveStatus.bPressureSPReached := i_stGenStatus.Bit4PressureSPReached;
stVcnVat590.stValveStatus.bWarningsActive := i_stGenStatus.Bit9WarningsActive;
//Read Remote status 
stVcnVat590.stValveStatus.eRemoteStatus.0 := i_stGenStatus.Bit7AccessModeBit0;
stVcnVat590.stValveStatus.eRemoteStatus.1 := i_stGenStatus.Bit8AccessModeBit1;
//Mapping General warnings
stVcnVat590.stValveStatus.bServiceRequest := i_stGenWarnings.Bit0ServiceRequest;
stVcnVat590.stValveStatus.bPowerFailureBattery := i_stGenWarnings.Bit3PowerFailureBattery;
stVcnVat590.stValveStatus.bSensMeasUnitFault := i_stGenWarnings.Bit10SensMeasUnitFault;
//Mapping Extended warnings
stVcnVat590.stValveStatus.bRemoteControlNotPossible := i_stExtnWarnings.Bit0RemoteControlNotPossible;
stVcnVat590.stValveStatus.bControlModeSPNotAllowed := i_stExtnWarnings.Bit1ControlModeSPNotAllowed;
stVcnVat590.stValveStatus.bZeroDisabled := i_stExtnWarnings.Bit2ZeroDisabled;
stVcnVat590.stValveStatus.bPFODeactivated := i_stExtnWarnings.Bit3PFODeactivated;
stVcnVat590.stValveStatus.bPressureSPOutOfRange := i_stExtnWarnings.Bit5PressureSPOutOfRange;
stVcnVat590.stValveStatus.bPositionSPOutOfRange := i_stExtnWarnings.Bit6PositionSPOutOfRange;
stVcnVat590.stValveStatus.bCtrlSPOutOfRange := i_stExtnWarnings.Bit10CtrlSPOutOfRange;
stVcnVat590.stValveStatus.bGeneralCtrlSPOutOfRange := i_stExtnWarnings.Bit11GeneralCtrlSPOutOfRange;
stVcnVat590.stValveStatus.bProcDataSettingsNotvalid := i_stExtnWarnings.Bit12ProcDataSettingsNotvalid;
]]></ST>
      </Implementation>
    </Action>
    <Method Name="SetThrottle" Id="{bc87ba94-efcb-4d7b-ace0-cb07482f478f}">
      <Declaration><![CDATA[METHOD SetThrottle : BOOL
VAR_INPUT
	rUpperLimit	:	REAL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stVcnVat590.fPosSetLimit := LIMIT(0, rUpperLimit, 100);]]></ST>
      </Implementation>
    </Method>
    <Method Name="ValveControl" Id="{25066385-eb5f-47fb-8ac4-a5fcdc50740f}">
      <Declaration><![CDATA[METHOD ValveControl : BOOL
VAR_INPUT
	state : E_VCN_VAT590; // Set Control modes
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stVcnVat590.eValveControl := state;]]></ST>
      </Implementation>
    </Method>
    <Action Name="WriteCoeParameters" Id="{9f7f239a-46a5-4609-b528-6b15a6fa6552}">
      <Implementation>
        <ST><![CDATA[// User will change one parameter at a time so solution with ELSIF should work OK
IF stVcnVat590.bCoeError THEN
	//fbCoeWrite.bExecute := FALSE;
	IF rtCoeReset.Q THEN
		stVcnVat590.bCoeError := FALSE;
	END_IF;
ELSIF bFirstRead THEN
	;//Wait until first first readout of all CoE parameters is completed
ELSIF stVcnVat590.stCoeInterface.nPressCtrl <> stCoeParameters.nPressCtrl THEN
	fbCoeWrite.nIndex := cPressCtrl.nIndex;
	fbCoeWrite.nSubIndex := cPressCtrl.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nPressCtrl);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nPressCtrl);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fSensorDelay_AD <> stCoeParameters.fSensorDelay_AD THEN
	fbCoeWrite.nIndex := cSensorDelay_AD.nIndex;
	fbCoeWrite.nSubIndex := cSensorDelay_AD.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fSensorDelay_AD);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fSensorDelay_AD);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fRampTime_AD <> stCoeParameters.fRampTime_AD THEN
	fbCoeWrite.nIndex := cRampTime_AD.nIndex;
	fbCoeWrite.nSubIndex := cRampTime_AD.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_AD);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_AD);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nRampMode_AD <> stCoeParameters.nRampMode_AD THEN
	fbCoeWrite.nIndex := cRampMode_AD.nIndex;
	fbCoeWrite.nSubIndex := cRampMode_AD.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_AD);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_AD);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fGainFactor_AD <> stCoeParameters.fGainFactor_AD THEN
	fbCoeWrite.nIndex := cGainFactor_AD.nIndex;
	fbCoeWrite.nSubIndex := cGainFactor_AD.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fGainFactor_AD);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fGainFactor_AD);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fRampTime_FIX1 <> stCoeParameters.fRampTime_FIX1 THEN
	fbCoeWrite.nIndex := cRampTime_FIX1.nIndex;
	fbCoeWrite.nSubIndex := cRampTime_FIX1.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_FIX1);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_FIX1);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nRampMode_FIX1 <> stCoeParameters.nRampMode_FIX1 THEN
	fbCoeWrite.nIndex := cRampMode_FIX1.nIndex;
	fbCoeWrite.nSubIndex := cRampMode_FIX1.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_FIX1);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_FIX1);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nCtrlDir_FIX1 <> stCoeParameters.nCtrlDir_FIX1 THEN
	fbCoeWrite.nIndex := cCtrlDir_FIX1.nIndex;
	fbCoeWrite.nSubIndex := cCtrlDir_FIX1.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nCtrlDir_FIX1);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nCtrlDir_FIX1);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fPGain_FIX1 <> stCoeParameters.fPGain_FIX1 THEN
	fbCoeWrite.nIndex := cPGain_FIX1.nIndex;
	fbCoeWrite.nSubIndex := cPGain_FIX1.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fPGain_FIX1);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_FIX1);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fIGain_FIX1 <> stCoeParameters.fIGain_FIX1 THEN
	fbCoeWrite.nIndex := cIGain_FIX1.nIndex;
	fbCoeWrite.nSubIndex := cIGain_FIX1.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fIGain_FIX1);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fIGain_FIX1);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fRampTime_FIX2 <> stCoeParameters.fRampTime_FIX2 THEN
	fbCoeWrite.nIndex := cRampTime_FIX2.nIndex;
	fbCoeWrite.nSubIndex := cRampTime_FIX2.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_FIX2);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_FIX2);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nRampMode_FIX2 <> stCoeParameters.nRampMode_FIX2 THEN
	fbCoeWrite.nIndex := cRampMode_FIX2.nIndex;
	fbCoeWrite.nSubIndex := cRampMode_FIX2.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_FIX2);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_FIX2);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nCtrlDir_FIX2 <> stCoeParameters.nCtrlDir_FIX2 THEN
	fbCoeWrite.nIndex := cCtrlDir_FIX2.nIndex;
	fbCoeWrite.nSubIndex := cCtrlDir_FIX2.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nCtrlDir_FIX2);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nCtrlDir_FIX2);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fPGain_FIX2 <> stCoeParameters.fPGain_FIX2 THEN
	fbCoeWrite.nIndex := cPGain_FIX2.nIndex;
	fbCoeWrite.nSubIndex := cPGain_FIX2.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fPGain_FIX2);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_FIX2);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fIGain_FIX2 <> stCoeParameters.fIGain_FIX2 THEN
	fbCoeWrite.nIndex := cIGain_FIX2.nIndex;
	fbCoeWrite.nSubIndex := cIGain_FIX2.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fIGain_FIX2);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fIGain_FIX2);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fRampTime_SP <> stCoeParameters.fRampTime_SP THEN
	fbCoeWrite.nIndex := cRampTime_SP.nIndex;
	fbCoeWrite.nSubIndex := cRampTime_SP.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fRampTime_SP);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fRampTime_SP);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.nRampMode_SP <> stCoeParameters.nRampMode_SP THEN
	fbCoeWrite.nIndex := cRampMode_SP.nIndex;
	fbCoeWrite.nSubIndex := cRampMode_SP.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.nRampMode_SP);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.nRampMode_SP);
	fbCoeWrite.bExecute := TRUE;
ELSIF stVcnVat590.stCoeInterface.fPGain_SP <> stCoeParameters.fPGain_SP THEN
	fbCoeWrite.nIndex := cPGain_SP.nIndex;
	fbCoeWrite.nSubIndex := cPGain_SP.nSubIndex;
	fbCoeWrite.pSrcBuf := ADR(stVcnVat590.stCoeInterface.fPGain_SP);
	fbCoeWrite.cbBufLen := SIZEOF(stVcnVat590.stCoeInterface.fPGain_SP);
	fbCoeWrite.bExecute := TRUE;
END_IF

ftCoeWriteBusy(CLK:=fbCoeWrite.bBusy);
IF ftCoeWriteBusy.Q THEN
	fbCoeWrite.bExecute := FALSE;
	IF fbCoeWrite.bError THEN
		stVcnVat590.bCoeError := TRUE;
	END_IF
END_IF

fbCoeWrite();]]></ST>
      </Implementation>
    </Action>
    <Action Name="WriteToOutput" Id="{d6e20cf1-2c45-411b-b669-d1005d8dcb97}">
      <Implementation>
        <ST><![CDATA[(*Write to EPICS*)
stVcnVat590.bIlkOK := bExtIlkOK;
stVcnVat590.nPositionSP := REAL_TO_DINT(fCalcPosSP);
stVcnVat590.nPressureSP := REAL_TO_DINT(fCalcPresSP);
stVcnVat590.fReqPosition := fReqPos;
stVcnVat590.fReqPressure := fReqPres;
stVcnVat590.fPresSetLimit := fPresMaxLim;
stVcnVat590.fPosSetLimit := fUpperLimit;

(*Write to VAT valve*)
o_eCtrlModeSP := eValveModeSP;
o_nRawPositionSP := REAL_TO_DINT(fCalcPosSP);
o_nRawPressureSP := REAL_TO_DINT(fCalcPresSP);
o_stGenCtrlSP.Bit0ExecuteZero := stVcnVat590.bZero;
o_stGenCtrlSP.Bit4AccessMode := bRemAcsToggle;]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_VCN_VAT590">
      <LineId Id="1200" Count="12" />
      <LineId Id="1218" Count="2" />
      <LineId Id="1228" Count="68" />
      <LineId Id="1573" Count="0" />
      <LineId Id="1299" Count="10" />
      <LineId Id="1554" Count="0" />
      <LineId Id="1550" Count="1" />
      <LineId Id="1555" Count="2" />
      <LineId Id="1570" Count="1" />
      <LineId Id="1569" Count="0" />
      <LineId Id="1384" Count="10" />
      <LineId Id="363" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.LoadPersistentData">
      <LineId Id="2" Count="5" />
      <LineId Id="17" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="22" Count="1" />
      <LineId Id="21" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.Logger">
      <LineId Id="2" Count="0" />
      <LineId Id="4" Count="25" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.ReadCoeParameters">
      <LineId Id="24" Count="1" />
      <LineId Id="145" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="142" Count="2" />
      <LineId Id="181" Count="0" />
      <LineId Id="141" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="67" Count="3" />
      <LineId Id="147" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="73" Count="2" />
      <LineId Id="72" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="77" Count="2" />
      <LineId Id="76" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="81" Count="2" />
      <LineId Id="80" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="85" Count="2" />
      <LineId Id="84" Count="0" />
      <LineId Id="151" Count="0" />
      <LineId Id="34" Count="0" />
      <LineId Id="89" Count="2" />
      <LineId Id="88" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="93" Count="2" />
      <LineId Id="92" Count="0" />
      <LineId Id="153" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="97" Count="2" />
      <LineId Id="96" Count="0" />
      <LineId Id="154" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="101" Count="2" />
      <LineId Id="100" Count="0" />
      <LineId Id="155" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="105" Count="2" />
      <LineId Id="104" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="109" Count="2" />
      <LineId Id="108" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="40" Count="0" />
      <LineId Id="113" Count="2" />
      <LineId Id="112" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="117" Count="2" />
      <LineId Id="116" Count="0" />
      <LineId Id="159" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="121" Count="2" />
      <LineId Id="120" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="125" Count="2" />
      <LineId Id="124" Count="0" />
      <LineId Id="161" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="129" Count="2" />
      <LineId Id="128" Count="0" />
      <LineId Id="182" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="133" Count="2" />
      <LineId Id="132" Count="0" />
      <LineId Id="162" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="137" Count="2" />
      <LineId Id="136" Count="0" />
      <LineId Id="163" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="146" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="184" Count="1" />
      <LineId Id="187" Count="0" />
      <LineId Id="190" Count="3" />
      <LineId Id="188" Count="1" />
      <LineId Id="186" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="165" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.ReadFromInput">
      <LineId Id="2" Count="1" />
      <LineId Id="38" Count="1" />
      <LineId Id="4" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="5" Count="12" />
      <LineId Id="35" Count="1" />
      <LineId Id="34" Count="0" />
      <LineId Id="18" Count="13" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.SetThrottle">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.ValveControl">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.WriteCoeParameters">
      <LineId Id="63" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="152" Count="0" />
      <LineId Id="154" Count="0" />
      <LineId Id="27" Count="2" />
      <LineId Id="24" Count="0" />
      <LineId Id="34" Count="3" />
      <LineId Id="26" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="64" Count="3" />
      <LineId Id="40" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="68" Count="3" />
      <LineId Id="43" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="73" Count="3" />
      <LineId Id="72" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="78" Count="3" />
      <LineId Id="77" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="83" Count="3" />
      <LineId Id="82" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="88" Count="3" />
      <LineId Id="87" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="98" Count="3" />
      <LineId Id="92" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="102" Count="3" />
      <LineId Id="97" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="107" Count="3" />
      <LineId Id="106" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="112" Count="3" />
      <LineId Id="111" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="117" Count="2" />
      <LineId Id="151" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="122" Count="3" />
      <LineId Id="121" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="127" Count="3" />
      <LineId Id="126" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="132" Count="3" />
      <LineId Id="131" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="137" Count="3" />
      <LineId Id="136" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="142" Count="3" />
      <LineId Id="141" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="147" Count="3" />
      <LineId Id="146" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="158" Count="1" />
      <LineId Id="175" Count="1" />
      <LineId Id="178" Count="2" />
      <LineId Id="177" Count="0" />
      <LineId Id="173" Count="0" />
      <LineId Id="157" Count="0" />
    </LineIds>
    <LineIds Name="FB_VCN_VAT590.WriteToOutput">
      <LineId Id="2" Count="5" />
      <LineId Id="17" Count="1" />
      <LineId Id="15" Count="1" />
      <LineId Id="10" Count="3" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>